<!DOCTYPE html>
<html>

	<head>
		<meta charset="utf-8">
		<title>着色器之loading</title>
		<script type="text/javascript" src="js/three.js"></script>
			<!--添加控制器-->
		<script src="js/controls/OrbitControls.js"></script>
		<style>
			body {
				margin: 0;
				overflow: hidden;
			}
		</style>
	</head>

	<body>
	
		
		<div id="container">
		</div>
		<!--ModelViewMatrix:模型矩阵 
		ProjectionMatrix:投影矩阵-->
		<script id="vertexShader" type="x-shader/x-vertex">

			varying vec2 vUv;

			void main()
			{
				vUv = uv;
				vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
				gl_Position = projectionMatrix * mvPosition;
			}

		</script>
		<script id="fragmentShader" type="x-shader/x-fragment">
		varying vec2 vUv;	
		uniform vec2 u_resolution;
		uniform vec2 u_mouse;
		uniform float u_time;
		uniform sampler2D iChannel0;
//Overly satisfying by nimitz (twitter: @stormoid)

//This might look like a lot of code but the base implementation of the gif itself is ~10loc

#define time iTime*1.2
#define pi 3.14159265

#define NUM 20.
#define PALETTE vec3(.0, 1.4, 2.)+1.5

#define COLORED
#define MIRROR
//#define ROTATE
#define ROT_OFST
#define TRIANGLE_NOISE

//#define SHOW_TRIANGLE_NOISE_ONLY

mat2 mm2(in float a){float c = cos(a), s = sin(a);return mat2(c,-s,s,c);}
float tri(in float x){return abs(fract(x)-.5);}
vec2 tri2(in vec2 p){return vec2(tri(p.x+tri(p.y*2.)),tri(p.y+tri(p.x*2.)));}
mat2 m2 = mat2( 0.970,  0.242, -0.242,  0.970 );

//Animated triangle noise, cheap and pretty decent looking.
float triangleNoise(in vec2 p)
{
    float z=1.5;
    float z2=1.5;
	float rz = 0.;
    vec2 bp = p;
	for (float i=0.; i<=3.; i++ )
	{
        vec2 dg = tri2(bp*2.)*.8;
        dg *= mm2(u_time*.3);
        p += dg/z2;

        bp *= 1.6;
        z2 *= .6;
		z *= 1.8;
		p *= 1.2;
        p*= m2;
        
        rz+= (tri(p.x+tri(p.y)))/z;
	}
	return rz;
}

void mainImage( )
{
   float aspect = u_resolution.x/u_resolution.y;
   float w = 50./sqrt(u_resolution.x*aspect+u_resolution.y);

    vec2 p = - 1.0 + 2.0 * vUv;
	p.x *= aspect;
    p*= 1.05;
    vec2 bp = p;
    
    #ifdef ROTATE
    p *= mm2(u_time*.25);
    #endif
    
    float lp = length(p);
    float id = floor(lp*NUM+.5)/NUM;
    
    #ifdef ROT_OFST
    p *= mm2(id*11.);
    #endif
    
    #ifdef MIRROR
    p.y = abs(p.y); 
    #endif
    
    //polar coords
    vec2 plr = vec2(lp, atan(p.y, p.x));
    
    //Draw concentric circles
    float rz = 1.-pow(abs(sin(plr.x*pi*NUM))*1.25/pow(w,0.25),2.5);
    
    //get the current arc length for a given id
    float enp = plr.y+sin((u_time+id*5.5))*1.52-1.5;
    rz *= smoothstep(0., 0.05, enp);
    
    //smooth out both sides of the arcs (and clamp the number)
    rz *= smoothstep(0.,.022*w/plr.x, enp)*step(id,1.);
    #ifndef MIRROR
    rz *= smoothstep(-0.01,.02*w/plr.x,pi-plr.y);
    #endif
    
    #ifdef TRIANGLE_NOISE
    rz *= (triangleNoise(p/(w*w))*0.9+0.4);
    vec3 col = (sin(PALETTE+id*5.+u_time)*0.5+0.5)*rz;
    col += smoothstep(.4,1.,rz)*0.15;
    col *= smoothstep(.2,1.,rz)+1.;
    
    #else
    vec3 col = (sin(PALETTE+id*5.+u_time)*0.5+0.5)*rz;
    col *= smoothstep(.8,1.15,rz)*.7+.8;
    #endif
    
    #ifndef COLORED
    col = vec3(dot(col,vec3(.7)));
    #endif
    
    #ifdef SHOW_TRIANGLE_NOISE_ONLY
    col = vec3(triangleNoise(bp));
    #endif
    
	gl_FragColor = vec4(col,1.0);
    
}


void main(){
	mainImage();
}
		</script>

		
		<script type="text/javascript">
			var container;
			var camera, scene, renderer,mesh;
			var uniforms;

			init();
			animate();

			function init() {
				container = document.getElementById('container');
	
				camera = new THREE.PerspectiveCamera(45, window.innerWidth / window.innerHeight, 0.1, 1000);
				camera.position.set(100,250,80)
			
				scene = new THREE.Scene();
				let c = new THREE.OrbitControls(camera)//设置控制器。方便操控3d世界。
				c.update()
				var geometry = new THREE.BoxBufferGeometry( 50,50,50 );

				uniforms = {
					u_time: {
						type: "f",
						value: 1.0
					},
					u_resolution: {
						type: "v2",
						value: new THREE.Vector2()
					},
					iChannel0: { value: new THREE.TextureLoader().load( './c.png' ) }//贴图跟着色器一起渲染
				};

				var material = new THREE.ShaderMaterial({
					uniforms: uniforms,
					vertexShader: document.getElementById('vertexShader').textContent,
					fragmentShader: document.getElementById('fragmentShader').textContent
				});

				mesh = new THREE.Mesh(geometry, material);
				var cloneM = mesh.clone()
				scene.add(mesh);

				renderer = new THREE.WebGLRenderer();
				renderer.setPixelRatio(window.devicePixelRatio);
				renderer.setClearColor(0xfefefe)
				container.appendChild(renderer.domElement);

				onWindowResize();
				window.addEventListener('resize', onWindowResize, false);
			}

			function onWindowResize(event) {
				renderer.setSize(window.innerWidth, window.innerHeight);
				uniforms.u_resolution.value.x = renderer.domElement.width;
				uniforms.u_resolution.value.y = renderer.domElement.height;
			}

			function animate() {
				requestAnimationFrame(animate);
				render();
			}
			function render() {
				uniforms.u_time.value += .1;
			//	mesh.rotation.y+=0.1
				renderer.render(scene, camera);
			}
//			setTimeout(()=>{
//				alert('哇，好炫酷啊，您将学这个')
//				
//				
//				alert('滚吧。你怎么可能学得会，关了吧')
//			},5000)
			
			
		</script>
	</body>